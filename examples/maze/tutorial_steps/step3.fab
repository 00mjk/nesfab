vars /game_vars
    CCC/levels level_ptr

// charmaps can be used to get a nice syntax for defining levels.
charmap(".#")

// The levels are defined here:
data /levels
    [] level1
        ("################################"
         "################################"
         "###.#######....#.#......########"
         "###.#...#.###.##.#...........###"
         "###...#.#...#..###......####.###"
         "#######.###.##...#.#########.###"
         "###.....#....#.###.#########.###"
         "###.#####.####.###.#########.###"
         "###.#......#.......#########.###"
         "###...#.####.#####...........###"
         "###.###.#........#.#######.#####"
         "###.....##########....####.#####"
         "###.#.#....#.#.....##.##.....###"
         "###.#.######.########.##.....###"
         "###.#...............#.##.....###"
         "###.###.#.#.####..#.#.####.#####"
         "###.#...###.#.##........##.#####"
         "#####.#.#...#.......###.##.#####"
         "###...#######.#########.##.#####"
         "###.###.....#.######....##.#####"
         "###.....#####.######....##.#####"
         "###.##.##.........##....##.#####"
         "######.#..##.#....###.####.#####"
         "###....#.##..#....###.##....####"
         "###.####.###.#....###.##....####"
         "###.#.##.#.#......###.......####"
         "###.#....#.#......#######.######"
         "###....#.#.######.#######.######"
         "################################"
         "################################")

fn load_level()
    // Tell the NES which VRAM address we want to upload to:
    ppu_reset_addr($2000)

    // Upload the tile data:
    for UU i = 0; i < 960; i += 1
        {PPUDATA}(level_ptr{i})

    // Then the attributes:
    for U i = 0; i < 64; i += 1
        {PPUDATA}(%00011011)

mode main()
    // Set the palette:
    palette = example_palette
    ppu_upload_palette()

    // Set and load the level:
    level_ptr = @level1
    load_level()

    // Turn on rendering:
    {PPUMASK}(PPUMASK_ON | PPUMASK_NO_CLIP)

    while true

// Define the tileset (commonly called CHR):
chrrom
    file(fmt, "tiles.png")
